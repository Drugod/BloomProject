ACTOR BlooMW : Weapon
{
	//$Category "BlooM Weapons"
	//$Color 14
	+INVENTORY.RESTRICTABSOLUTELY
	Inventory.RestrictedTo "DoomDude"
	VisibleToPlayerClass "DoomDude"
}

ACTOR ------GRENADES------------- {}
ACTOR HandGrenades : BlooMW
{
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 0
	Weapon.SelectionOrder 99999
	Weapon.AmmoType1 "GrenadeAmmo"
	Weapon.AmmoType2 "GrenadeAmmo"
	Obituary "$OB_FRAG"
    AttackSound "None"
    Inventory.PickupSound "AmmoAPIN"
	Inventory.PickupMessage "$FRAGPICK"
    +WEAPON.NOAUTOAIM
    +WEAPON.NOALERT
	+WEAPON.NOAUTOFIRE
    Scale 0.3
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("blm_voxmod")==1,"ModelSpawn")
	BaseSpawn:
		PGRN D 1
		PGRN D -1 A_SpawnItemEx("HappyBundle",0,0,0)
		Stop
	ModelSpawn:
		UNKN A 1
		UNKN A -1 A_SpawnItemEx("HappyBundle",0,0,0)
		Stop
	Ready:
		TNT1 A 0 A_TakeInventory("ThrowPower", 150)
	    TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("GrenadeAmmo", 1, 1)
		Goto NoGrenades
		HNDG AB 1
		Goto Steady
	Steady:
		HNDG A 1 A_WeaponReady
		Loop
	NoGrenades:
		TNT1 A 1 A_WeaponReady
		Goto Ready
	Deselect:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("FiredGrenade", 1, "DropGrenade")
		TNT1 A 1 A_Lower
		Wait
	DropGrenade:
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("FiredGrenade", 1)
		TNT1 A 0 A_FireCustomMissile("HandGrenadeDroped", frandom(-2,2), 1, 0, 0, 0, 0)
		Goto Deselect
	Select:
		HNDG A 1 A_Raise
		Loop
	Fire:
	    HNDG ABCD 1
		TNT1 A 0 A_GiveInventory("FiredGrenade", 1)
		TNT1 A 0 A_PLaySound ("grenades/grnpin")
		HNDG EEFG 1
	Hold:
	    TNT1 A 1 A_GiveInventory("ThrowPower", 1)
		TNT1 A 1 A_Refire
		TNT1 A 0 A_PLaySound ("grenades/grntoss")
		HNDG HI 1
		TNT1 A 0 A_FireCustomMissile("HandGrenade", frandom(-2,2), 1, 0, 0, 0, 0)
	    TNT1 A 0 A_TakeInventory("FiredGrenade", 1)
		HNDG JKLMNOPQ 3
		TNT1 A 3
		Goto Ready
	AltFire:
	    HNDG ABCD 1
		TNT1 A 0 A_GiveInventory("FiredGrenade", 1)
		TNT1 A 0 A_PLaySound ("grenades/grnpin")
		HNDG EEFG 1
	AltHold:
	    TNT1 A 1
		TNT1 A 1 A_Refire
		TNT1 A 0 A_PLaySound ("grenades/grntoss")
		HNDG HI 1
		TNT1 A 0 A_FireCustomMissile("HandGrenadeAlt", 0, 1, 0, 0, 0, 0)
	    TNT1 A 0 A_TakeInventory("FiredGrenade", 1)
		HNDG JKLM 1
		HNDG NOPQ 1
		TNT1 A 3
		Goto Ready
	TossGrenade:
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("TossGrenade", 1)
		TNT1 A 0 A_JumpIfInventory("GrenadeAmmo", 1, 1)
		Goto NoGrenade
		HNDG ABCD 1
		TNT1 A 0 A_GiveInventory("FiredGrenade", 1)
		TNT1 A 0 A_PLaySound ("grenades/grnpin")
		HNDG EEFG 1
		TNT1 A 0 A_PLaySound ("grenades/grntoss")
		HNDG HI 1
		TNT1 A 0 A_TakeInventory("GrenadeAmmo", 1)
		TNT1 A 0 A_FireCustomMissile("HandGrenade", frandom(-2,2), 0, 0, 0, 0, 0)
	    TNT1 A 0 A_TakeInventory("FiredGrenade", 1)
		HNDG JKLM 1
		HNDG NOPQ 1
		TNT1 A 1
		TNT1 A 0 A_TakeInventory("TossGrenade", 1)
		Goto Ready
	NoGrenade:
		TNT1 A 0
		TNT1 A 0 A_Print("No Grenades Left.")
		Goto Ready+8
	}
}

ACTOR GrenadeTime : Inventory { Inventory.MaxAmount 6 }
ACTOR TossGrenade : Inventory { Inventory.MaxAmount 1 }
ACTOR FiredGrenade : Inventory { Inventory.MaxAmount 1 }

ACTOR HandGrenade
{
	Radius 4
	Height 4
	Projectile
	Speed 30
	Damage 0
	Gravity 0.7
	Scale 0.2
	Projectile
	+DOOMBOUNCE
	+EXPLODEONWATER
	+MISSILE
	+SKYEXPLODE
	-BLOODSPLATTER
	-EXTREMEDEATH
	-NOGRAVITY
	BounceFactor 0.5
	WallBounceFactor 0.25
	Health 5
	SeeSound "grenades/grnbnce"
	DeathSound "none"
	Obituary "$OB_HAND"
	States
	{
	Spawn:
		HGRN ABCDEF 2
		TNT1 A 0 A_GiveInventory("GrenadeTime", 1)
		TNT1 A 0 A_JumpIfInventory("GrenadeTime", 3, "Explode")
		Loop
	Death:
		TNT1 A 0
		HGRN F 16
		TNT1 A 0 A_GiveInventory("GrenadeTime", 1)
		TNT1 A 0 A_JumpIfInventory("GrenadeTime", 3, "Explode")
		Loop
	Explode:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_CustomMissile("FloorKaboom",0,0,0,CMF_TRACKOWNER)
		TNT1 A 0 A_Explode(200, 150, XF_HURTSOURCE)
		TNT1 A 0 A_Playsound("MediumExplosion")
		TNT1 A 0 Radius_Quake (2, 24, 0, 15, 0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SpawnItemEx ("BigSmoke",0,0,0)
		Stop
	}
}

ACTOR HandGrenadeDroped: HandGrenade { Speed 4 }
ACTOR HandGrenadeAlt: HandGrenade { Speed 8 }

ACTOR GrenadeAmmo : Ammo 6666
{
	//$Category "BlooM Ammunitions"
	//$Sprite PGRND0
    +INVENTORY.RESTRICTABSOLUTELY
    Tag "Hand Grenade"
    Inventory.ForbiddenTo "Caleb"
    VisibleToPlayerClass "DoomDude"
	Inventory.PickupMessage "$GRENPICK"
	Inventory.Amount 1
	Inventory.MaxAmount 10
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 20
	Inventory.Icon "PGRND0"
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("blm_voxmod")==1,"ModelSpawn")
	BaseSpawn:
		PGRN D 1
		PGRN D -1 A_SpawnItemEx("HappyBundle",0,0,0)
		Stop
	ModelSpawn:
		UNKN A 1
		UNKN A -1 A_SpawnItemEx("HappyBundle",0,0,0)
		Stop
	}
}

ACTOR GrenadeBox : GrenadeAmmo
{
	//$Sprite GRNDH0
    +INVENTORY.RESTRICTABSOLUTELY
    Inventory.ForbiddenTo "Caleb"
    VisibleToPlayerClass "DoomDude"
	Inventory.PickupMessage "$GRENBOXP"
	Inventory.Amount 10
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("blm_voxmod")==1,"ModelSpawn")
	BaseSpawn:
		PGRN D -1
		Stop
	ModelSpawn:
		UNKN A -1
		Stop
	}
}

ACTOR --------FLAMETHROWER------------ {}
ACTOR FlameThrower_ : BlooMW 19074
{
	//$Sprite PMAWA0
	Tag "UAC FlameThrower"
	Weapon.SelectionOrder 3000
	Weapon.AmmoType "FlameGas"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 64
	Inventory.PickupMessage "$FTHRPICK"
	Inventory.PickupSound "flamethrower/Pick"
	Obituary "OB_FTHRW"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("blm_voxmod")==1,"ModelSpawn")
	BaseSpawn:
		PMAW A 1
		PMAW A -1 A_SpawnItemEx("Spraycan",0,0,0)
		Stop
	ModelSpawn:
		UNKN A 1
		UNKN A -1 A_SpawnItemEx("Spraycan",0,0,0)
		Stop
	Ready:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIf(waterlevel>2,"Cannot")
		PMAC AAAABBBBCCCCDDDDEEEE 1 A_WeaponReady
		Loop
	Deselect:
		PMAA HHGGFFEEDDCCBBAA 1
		PMAA A 0 A_StopSound(CHAN_BODY)
		TNT1 AAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait
	Select:
		TNT1 AAAAA 1 A_Raise
		TNT1 AAAAAAAAAA 0 A_Raise
		Goto SelectAnimation
	SelectAnimation:
		PMAA AABBCCDDEEFFGGHH 1
		Goto Ready
	Fire:
		TNT1 A 0 A_PlayWeaponSound ("flamethrower/Fire")
		PMCC ABCDEFGHIJKLMN 1 A_FireCustomMissile("FlamethrowFire",0+frandom(-1,1),1,0+frandom(-1,1),4+frandom(-1,1))
		PMCR AAAABBBBCCCCDDDDEEEEFFFFGGGGHHHH 1 A_ReFire
		Goto Ready
	Cannot:
		TNT1 A 1 A_SelectWeapon("Fist_")
		Goto Deselect
	TossGrenade:
		TNT1 A 0
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("TossGrenade", 1)
		TNT1 A 0 A_JumpIfInventory("GrenadeAmmo", 1, 1)
		Goto NoGrenade
		HNDG ABCD 1
		TNT1 A 0 A_GiveInventory("FiredGrenade", 1)
		TNT1 A 0 A_Playsound ("GRNPIN")
		HNDG EEFG 1
		TNT1 A 0 A_Playsound ("GRNTOSS")
		HNDG HI 1
		TNT1 A 0 A_TakeInventory("GrenadeAmmo", 1)
		TNT1 A 0 A_FireCustomMissile("HandGrenade", frandom(-2,2), 0, 0, 0, 0, 0)
		TNT1 A 0 A_TakeInventory("FiredGrenade", 1)
		HNDG JKLM 1
		TNT1 A 1
		TNT1 A 0 A_TakeInventory("TossGrenade", 1)
		Goto Ready
	NoGrenade:
		TNT1 A 0
		TNT1 A 0 A_Print("$NOGREN")
		Goto Ready
	}
}

ACTOR FlameGas : Ammo 23001
{
	//$Category "BlooM Ammunitions"
	//$Title "Flamethrower Canister"
	//$Sprite AGASA0
    +INVENTORY.RESTRICTABSOLUTELY
	Inventory.Amount 24
	Inventory.MaxAmount 240
    VisibleToPlayerClass "DoomDude"
    Inventory.ForbiddenTo "Caleb"
	Ammo.BackpackAmount 24
	Ammo.BackpackMaxAmount 480
	Inventory.PickupMessage "$FLAGAS"
	Inventory.Icon "AGASA0"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("blm_voxmod")==1,"ModelSpawn")
	BaseSpawn:
		AGAS A 1
		AGAS A -1 A_SpawnItemEx("SprayCanisterPickup",0,0,0)
		Stop
	ModelSpawn:
		UNKN A 1
		UNKN A -1 A_SpawnItemEx("SprayCanisterPickup",0,0,0)
		Stop
	}
}

ACTOR FlameBigGas : FlameGas 23002
{
	//$Title "Flamethrower Big Canister"
	//$Sprite AGASB0
	Inventory.Amount 120
	Inventory.PickupMessage "$FLABIG"
	Inventory.Icon "AGASB0"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("blm_voxmod")==1,"ModelSpawn")
	BaseSpawn:
		AGAS B 1
		AGAS B -1 A_SpawnItemEx("GasolineCan",0,0,0)
		Stop
	ModelSpawn:
		UNKN A 1
		UNKN A -1 A_SpawnItemEx("GasolineCan",0,0,0)
		Stop
	}
}

ACTOR --------FROSTGUN------------ {}
ACTOR Frostgun_ : BlooMW 19076
{
	//$Sprite FROSA0
	Tag "UAC FrostGun"
	Weapon.SelectionOrder 3000
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "FrostAmmo"
	Inventory.PickupMessage "$FROSPICK"
	Inventory.PickupSound "frostgun/Pick"
	Obituary "OB_FROST"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("blm_voxmod")==1,"ModelSpawn")
	BaseSpawn:
		FROS A 1
		FROS A -1 A_SpawnItemEx("TeslaRifle",0,0,0)
		Stop
	ModelSpawn:
		UNKN A 1
		UNKN A -1 A_SpawnItemEx("TeslaRifle",0,0,0)
		Stop
	Ready:
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		FRST ABCDEFGHIJ 1 A_WeaponReady
		Loop
	Deselect:
		FRST JJIIHHGGFFEEDDCCBBAA 1 A_Lower
		FRST A 0 A_StopSound(CHAN_BODY)
		Wait
	Select:
		FRST AABBCCDDEEFFGGHHIIJJ 1 A_Raise
		Goto Ready
	Fire:
		FRSF AB 1 A_PlayWeaponSound ("frostgun/Fire")
		FRSF C  1 A_FireCustomMissile("FrostPlasma",0,1,0,0)
		TNT1 A  0 A_Quake(2,6,0,100)
		FRSF C  1 A_FireCustomMissile("FrostPlasma",0,1,0,0)
		TNT1 A  0 A_Quake(2,6,0,100)
		FRSF C  1 A_FireCustomMissile("FrostPlasma",0,1,0,0)
		TNT1 A  0 A_Quake(2,6,0,100)
		FRSF C  1 A_FireCustomMissile("FrostPlasma",0,1,0,0)
		TNT1 A  0 A_Quake(2,6,0,100)
		FRSF C  1 A_FireCustomMissile("FrostPlasma",0,1,0,0)
		TNT1 A  0 A_Quake(2,6,0,100)
		FRSF C  1 A_FireCustomMissile("FrostPlasma",0,1,0,0)
		TNT1 A  0 A_Quake(2,6,0,100)		
		FRSF DEFGHIJ 1
		FRSR AAAABBBBCCCCDDDDEEEEFFFFGGGGHHIIJJKKLL 1
		FRSF A 5 A_ReFire
		Goto Ready
	TossGrenade:
		TNT1 A 0
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("TossGrenade", 1)
		TNT1 A 0 A_JumpIfInventory("GrenadeAmmo", 1, 1)
		Goto NoGrenade
		HNDG ABCD 1
		TNT1 A 0 A_GiveInventory("FiredGrenade", 1)
		TNT1 A 0 A_Playsound ("GRNPIN")
		HNDG EEFG 1
		TNT1 A 0 A_Playsound ("GRNTOSS")
		HNDG HI 1
		TNT1 A 0 A_TakeInventory("GrenadeAmmo", 1)
		TNT1 A 0 A_FireCustomMissile("HandGrenade", frandom(-2,2), 0, 0, 0, 0, 0)
		TNT1 A 0 A_TakeInventory("FiredGrenade", 1)
		HNDG JKLM 1
		TNT1 A 1
		TNT1 A 0 A_TakeInventory("TossGrenade", 1)
		Goto Ready
	NoGrenade:
		TNT1 A 0
		TNT1 A 0 A_Print("$NOGREN")
		Goto Ready
	}
}

ACTOR FrostAmmo : Ammo 23003 //requires Caleb replacement codewise yet
{
	//$Category "BlooM Ammunitions"
	//$Title "Frostgun Ammo"
	//$Sprite FROPA0
	Inventory.Amount 4
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 4
	Ammo.BackpackMaxAmount 100
	Inventory.PickupMessage "$FROSTA"
	Inventory.Icon "FROPA0"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("blm_voxmod")==1,"ModelSpawn")
	BaseSpawn:
		FROP DDAABBCC 4
		Loop
	ModelSpawn:
		UNKN A -1
		Stop
	}
}

ACTOR --------TESLACANNON------------ {}
ACTOR TeslaCannon_ : BlooMW 19075
{
	//$Sprite TEDWA0
	Tag "UAC Tesla Cannon"
	Weapon.SelectionOrder 3000
	Weapon.AmmoType1 "TeslaCharge"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "TeslaCharge"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 64
	Inventory.PickupMessage "$TESLPICK"
	Obituary "OB_TESDM"
	+WEAPON.AMMO_OPTIONAL
    +WEAPON.ALT_AMMO_OPTIONAL
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("blm_voxmod")==1,"ModelSpawn")
	BaseSpawn:
		TEDW A 1
		TEDW A -1 A_SpawnItemEx("TeslaRifle",0,0,0)
		Stop
	ModelSpawn:
		UNKN A 1
		UNKN A -1 A_SpawnItemEx("TeslaRifle",0,0,0)
		Stop
	Ready:
		TNT1 A 0
		TNT1 A 0 A_Jump(64,"Ready2","Ready3","Ready4")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TEDM ABCDEFGHIJKLMNOPQRSTUVWXYZYXWVUTSRQPONMLKJIHGFEDCBA 1 A_WeaponReady
		Loop
	Ready2:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TEDM ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		TEDN ABCDEFGH 1 A_WeaponReady
		Goto Ready
	Ready3:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TEDO ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		Goto Ready
	Ready4:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TEDO ZYXWVUTSRQPONMLKJIHGFEDCBA 1 A_WeaponReady
		Goto Ready
	Deselect:
		TEDM ABCDEFGHIJKLMNOPQRSTUVWXYZYXWVUTSRQPONMLKJIHGFEDCBA 1 A_Lower
		Loop
	Select:
		TEDM ABCDEFGHIJKLMNOPQRSTUVWXYZYXWVUTSRQPONMLKJIHGFEDCBA 1 A_Raise
		Loop
	AltFire:
		TNT1 A 0
		TNT1 A 0 A_JumpIfNoAmmo("FirelessAlt")
		TEDQ ABCDEFGHIJKLMNOPQRST 1 Bright A_Quake(2,6,0,100)
 		TNT1 A 0 A_Quake(2,6,0,100)
		TNT1 A 0 A_JumpIf(waterlevel>= 2,"Suicide")
		TEDR ABCDEFGHIJKLMNOPQRST 1 Bright A_FireCustomMissile("TeslaProjectile",0.0,1,0.0,-20.0)
		TEDO ZYXWVUTSRQPONMLKJIHGFEDCBA 1 A_ReFire
		Goto Ready
	Fire:
		TNT1 A 0
		TNT1 A 0 A_JumpIfNoAmmo("Fireless")
		TEDQ A 1 Bright A_FireCustomMissile("TeslaProjectile",0.0,1,0.0,-20.0)
		TNT1 A 0 A_JumpIf(waterlevel>= 2,"Suicide")
		TEDQ BCD 1 Bright A_Quake(2,6,0,100)
		TEDQ E 1 Bright A_FireCustomMissile("TeslaProjectile",0.0,1,0.0,-20.0)
		TEDQ FGH 1 Bright A_Quake(2,6,0,100)
		TEDQ I 1 Bright A_FireCustomMissile("TeslaProjectile",0.0,1,0.0,-20.0)
		TEDQ JKL 1 Bright A_Quake(2,6,0,100)
		TEDQ M 1 Bright A_FireCustomMissile("TeslaProjectile",0.0,1,0.0,-20.0)
		TEDQ NOP 1 Bright A_Quake(2,6,0,100)
		TEDQ Q 1 Bright A_FireCustomMissile("TeslaProjectile",0.0,1,0.0,-20.0)
		TEDQ RST 1 Bright A_Quake(2,6,0,100)
		TEDO ZYXWVUTSRQPONMLKJIHGFEDCBA 1
		Goto Ready
	Fireless:
		TEME ABCDE 1
		TNT1 A 0 A_JumpIf(waterlevel>= 2,"Suicide")
		TEME F 1 Bright
	FirelessHold:
		TNT1 A 0 A_JumpIf(waterlevel>= 2,"Suicide")
		TEME G 1 Bright A_Quake(1,3,0,100)
		TEME H 1 Bright A_CustomPunch(random(4,16),0,CPF_NORANDOMPUFFZ,"TeslaPuff")
		TEME IJKLMN 1 A_ReFire("FirelessHold")
		Goto Ready
	FirelessAlt:
		TEMF ABCDE 1
		TNT1 A 0 A_JumpIf(waterlevel>= 2,"Suicide")
		TEMF F 1 Bright
	FirelessAltHold:
		TNT1 A 0 A_JumpIf(waterlevel>= 2,"Suicide")
		TEMF G 1 Bright A_Quake(1,3,0,100)
		TEMF H 1 Bright A_CustomPunch(random(4,16),0,CPF_NORANDOMPUFFZ,"TeslaPuff")
		TEMF IJKLMN 1 A_ReFire("FirelessAltHold")
		Goto Ready
	Flash:
		TNT1 A 4 BRIGHT A_Light1
		Goto LightDone
		TNT1 A 4 BRIGHT A_Light2
		Goto LightDone
	Suicide:
		TNT1 A 0 A_PlayWeaponSound ("TESLWI")
		TNT1 A 0 A_SetBlend("Purple",0.25,5)
		TEDM L 2 A_SetBlend("Purple",0.75,5)
		TEDM M 2 A_SetBlend("Purple",1.25,5)
		TEDM N 2 A_SetBlend("Purple",2.75,5)
		TNT1 A 0 A_PlayWeaponSound ("TESLWIX")
		TEDM M 1 A_SetBlend("White",3.25,5)
		TEDM L 2 A_SetBlend("White",4.75,5)
		TEDM M 2 A_SetBlend("White",5.25,5)
		TEDM N 2 A_SetBlend("White",6.75,5)
		TEDM M 1 A_SetBlend("White",7.25,5)
		TNT1 A 0 A_GunFlash
		TEDM L 1 A_Explode(9999, 1024, 1)
		Goto Ready
	TossGrenade:
		TNT1 A 0
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("TossGrenade", 1)
		TNT1 A 0 A_JumpIfInventory("GrenadeAmmo", 1, 1)
		Goto NoGrenade
		HNDG ABCD 1
		TNT1 A 0 A_GiveInventory("FiredGrenade", 1)
		TNT1 A 0 A_Playsound ("GRNPIN")
		HNDG EEFG 1
		TNT1 A 0 A_Playsound ("GRNTOSS")
		HNDG HI 1
		TNT1 A 0 A_TakeInventory("GrenadeAmmo", 1)
		TNT1 A 0 A_FireCustomMissile("HandGrenade", frandom(-2,2), 0, 0, 0, 0, 0)
		TNT1 A 0 A_TakeInventory("FiredGrenade", 1)
		HNDG JKLM 1
		TNT1 A 1
		TNT1 A 0 A_TakeInventory("TossGrenade", 1)
		Goto Ready
	NoGrenade:
		TNT1 A 0
		TNT1 A 0 A_Print("$NOGREN")
		Goto Ready
	}
}

ACTOR TeslaPuff: FistPuff
{
	DamageType "Shock"
	States
	{
	Spawn:
	Melee:
		TNT1 A 0
	Crash:
        TNT1 AAAAA 0 A_SpawnItemEx("TeslaFX", random(-8,8), random(-8,8), random(0,3), random(-12,12), random(-12,12), random(0,3))
		Stop
	}
}