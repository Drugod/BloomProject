ACTOR ------COMMON-ELEMENTS---------- {}
ACTOR BlooMClass : Inventory
{
	+IGNORESKILL
	+UNDROPPABLE
	Inventory.Amount 1
	Inventory.MaxAmount 3
}

ACTOR -------PLAYERS--------- {}
ACTOR Caleb : PlayerPawn
{
	Mass 35
	XScale 0.6
	YScale 0.52
	DamageFactor "Blast", 0.444
	DamageFactor "CALINOimmune",0
	DamageFactor "Flare", 3
	DamageFactor "Shock", 0.75
	DamageFactor "StunningHead", 0.1
	PainChance "KickableHead",0
	Player.ColorRange 112, 127
	Player.ColorSet 0, "Green",         0x70, 0x7F,  0x72
	Player.ColorSet 1, "Gray",          0x60, 0x6F,  0x62
	Player.ColorSet 2, "Brown",         0x40, 0x4F,  0x42
	Player.ColorSet 3, "Red",           0x20, 0x2F,  0x22
	Player.ColorSet 4, "Light Gray",    0x58, 0x67,  0x5A
	Player.ColorSet 5, "Light Brown",   0x38, 0x47,  0x3A
	Player.ColorSet 6, "Light Red",     0xB0, 0xBF,  0xB2
	Player.ColorSet 7, "Light Blue",    0xC0, 0xCF,  0xC2
	Player.CrouchSprite "CALY"
	Player.DisplayName "Caleb"
	Player.Face "CTF"
	Player.ForwardMove 0.88 0.82
	Player.JumpZ 10
	Player.Portrait "P_CALEB"
	Player.Soundclass "Caleb"
	Player.StartItem "BlooMClass", 1
	Player.StartItem "Colt"
	Player.StartItem "Pitchfork"
	Player.StartItem "TommyClip", 50
	Player.WeaponSlot 0, "VoodooDoll"
	Player.WeaponSlot 1, "PitchFork"
	Player.WeaponSlot 2, "Flaregun","Colt"
	Player.WeaponSlot 3, "SingleShotgun","Sawedoff"
	Player.WeaponSlot 4, "TommyGun"
	Player.WeaponSlot 5, "NapalmLauncher"
	Player.WeaponSlot 6, "ProximityDynamite","RemoteDynamite","Dynamite"
	Player.WeaponSlot 7, "SprayCan"
	Player.WeaponSlot 8, "TeslaRifle"
	Player.WeaponSlot 9, "LifeLeech"
    States
    {
    Spawn:
		TNT1 A 0
		CALB I -1
		Loop
	See:
		CALB AAAABBBBCCCCDDDDEEEEFFFF 1
		Loop
	Missile:
		CALB IIII 1
		Goto Spawn
	Melee:
		TNT1 A 0 A_JumpIfInventory("UsingPitchfork", 1, "Missile")
		CALB GGGGGG 1
		Goto Missile
	Pain.GOUPBOY:
		CALB H 0 ThrustThingZ(0,70,0,1)
	Pain:
		CALB H 0 A_JumpIfInventory("BurnTics",1,"Pain.Fire")
		CALB HHHH 1
		TNT1 A 0 A_Pain
		CALB HHHH 1
		Goto Spawn
	Pain.SpecialFire:
		CALB G 0 //ACS_ExecuteAlways(618,0,24,1)
		Goto Pain.Fire+1
	Pain.Burn:
	Pain.Lava:
	Pain.Fire:
		CALB G 0 A_GiveInventory("BurnSound",1)
		CALB G 0 A_JumpIf(waterlevel>1,"SwimThaw")
		CALB G 0 A_JumpIfInventory("BurnSound",9,"Pain.SoundBack")
		CALB G 0 A_JumpIfInventory("BurnSound",2,"Pain.OnFire")
		CALB G 1 A_PlaySound("Caleb/Burns",CHAN_VOICE)
		Goto See
	Pain.OnFire:
		CALB G 1 A_SetBlend("Red",0.1,30)
		Goto See
	Pain.SoundBack:
		CALB G 0 A_TakeInventory("BurnSound",90)
		Goto Pain.Fire
	Pain.Blast:
	Pain.Dynamite:
	Pain.Napalm:
		CALB G 0 A_TakeInventory("ChokeMe",1)
	Pain.CultistTNT:
		CALE G 0 A_TakeInventory("ArmorPicker",411)	//reset token to zero
		CALE G 0 A_GiveInventory("ArmorPicker",1)	//ACS token that says "just damaged by fire"
		Goto See
	SwimThaw:
		CALE A 4 A_GiveInventory("Swimming",1)
		CALE B 0 A_TakeInventory("BurnSound",90)
		CALE BCD 4
		CALE A 0 A_JumpIf(Waterlevel<1,"StopSwim")
		Loop
	StopSwim:
		CALE A 4 A_TakeInventory("Swimming",99)
		Goto See
	Dill:
		CALE G 0 A_SetBlend("Orange",0.15,100)
		Goto See
	Pain.BlindingPoison:
		CALE G 0 A_SetBlend("Black",0.85,190)
		CALE G 0 ACS_NamedExecuteAlways("StrongPoison",0)
		Goto Pain
	Pain.TripWire:
	Pain.NumbnessX:
		CALE G 0 A_GiveInventory("CurseNumbness",35)
		CALE G 0 A_SetBlend("Black",0.6,80)
		Goto See
	Pain.Numbness:
		CALE G 0 A_GiveInventory("CurseNumbness",random(4,9))
		CALE G 0 A_Pain
		Goto See
	Pain.FragilityX:
		CALE G 0 A_GiveInventory("CurseFragility",29)
	Pain.Fragility:
		CALE G 0 A_GiveInventory("CurseFragility",random(1,2))
		CALE G 0 A_Pain
		Goto See
	Pain.Disintegrate:
	Pain.InsaneBleed: //Damaged by phantasm
		TNT1 A 0 A_Pain
		CALB HH 0 ACS_NamedExecuteAlways("PlSndDamageMelee",0,90)
	Pain.Spiritual:
		CALE G 0 A_TakeInventory("ArmorPicker",411)	//reset token to zero
		CALE G 0 A_GiveInventory("ArmorPicker",2)	//ACS token that says "just damaged by magic"
		Goto See
	Pain.Turner:
		CALE G 0 ACS_NamedExecuteAlways("PoisonCheck")
		Goto Pain
	Pain.Stun: // Gargoyles
		Goto Pain
	Pain.Axe:
		CALB H 0 ACS_NamedExecuteAlways("PlSndDamageMelee",0,21)
		Goto Pain
	Pain.ChokeEM:
		PLAY G 1 A_GiveInventory("ChokeMe",1)
		TNT1 A 1 A_PlaySound("Hand/Choke")
		Goto See
	Pain.Silently:
		PLAY G 1 A_PlaySound("Null")
		Goto See
	Death:
		//CALB A 0 ACS_ExecuteAlways(621,0)
		TNT1 A 0 A_NoBlocking
		CALB JJJJJJJJJJ 1
		TNT1 A 0 A_Playsound("Caleb/Death")
		CALB KKKKKKKKKK 1
		CALB LLLLLLLLLL 1
		CALB MMMMMMMMMMNNNNNNNNNN 1
		CALB O -1
		Stop
	Death.Cultist:
		TNT1 A 0 A_SpawnItem("CultistSounder")
		Goto Death
	Death.CultistTNT:
		TNT1 A 0 A_SpawnItem("CultistSounder")
		Goto XDeath
	Death.GOUPBOY:
	XDeath:
		CALX A 0 A_NoBlocking
		CALX A 7 A_TakeInventory("Chokeme",100)
		CALX B 8 A_Playsound("Caleb/Death")
		CALX C 8
		CALX DEFGHIJK 8
		CALX L -1
		Stop
	Pain.LifeLeech:
		TNT1 A 0 A_GiveToTarget("LifeLeechHealth", random(40,50))
		Goto Pain
	Death.LifeLeech:
		TNT1 A 0 A_GiveToTarget("LifeLeechHealth", 100)
	Death.SpecialFire:
	Death.Flare:
	Burn:
		TNT1 A 1 A_Playsound("BurnDeath")
    	BZOM I 0 A_Fall
    	BZOM I 0 A_SpawnItem("BurnDeath1",1)
    	TNT1 A -1
		Stop
	Death.ChokeEM:
		PLAY H 1 A_TakeInventory("ChokeMe",1)
		PLAY H 1 A_GiveInventory("AmAlreadyChoked",1)
		PLAY H 1 A_SpawnItemEx ("ChokingHand",10,50,30,0,0,0,0)
		Goto Death
    }
}

ACTOR DoomDude : PlayerPawn
{
	Health 100
	Height 56
	Radius 16
	Mass 100
	Speed 1
	DamageFactor "Blast", 0.444
	DamageFactor "Flare", 3
	DamageFactor "Shock", 0.75
	DamageFactor "StunningHead", 0.1
	PainChance 255
	Player.ColorRange 112, 127
	Player.ColorSet 0, "Green",         0x70, 0x7F,  0x72
	Player.ColorSet 1, "Gray",          0x60, 0x6F,  0x62
	Player.ColorSet 2, "Brown",         0x40, 0x4F,  0x42
	Player.ColorSet 3, "Red",           0x20, 0x2F,  0x22
	Player.ColorSet 4, "Light Gray",    0x58, 0x67,  0x5A
	Player.ColorSet 5, "Light Brown",   0x38, 0x47,  0x3A
	Player.ColorSet 6, "Light Red",     0xB0, 0xBF,  0xB2
	Player.ColorSet 7, "Light Blue",    0xC0, 0xCF,  0xC2
	Player.CrouchSprite "PLYC"
	Player.DisplayName "Marine"
	Player.ForwardMove 0.88 0.82
	Player.JumpZ 10
	Player.Portrait "P_DGUY"
	Player.SoundClass "DoomDude"
	Player.StartItem "BlooMClass", 2
	Player.StartItem "Clip_", 50
	Player.StartItem "Fist_"
	Player.StartItem "GrenadeAmmo", 2
	Player.StartItem "Pistol_"
	Player.WeaponSlot 1, "Fist_", "Chainsaw_"
	Player.WeaponSlot 2, "Pistol_"
	Player.WeaponSlot 3, "Shotgun_", "SuperShotgun_"
	Player.WeaponSlot 4, "Chaingun_"
	Player.WeaponSlot 5, "RocketLauncher_"
	Player.WeaponSlot 6, "PlasmaRifle_"
	Player.WeaponSlot 7, "BFG9000_"
	Player.WeaponSlot 8, "HandGrenades"
	States
	{
	Spawn:
		PLAY A -1
		Loop
	See:
		PLAY ABCD 4
		Loop
	Missile:
		PLAY E 12
		Goto Spawn
	Melee:
		PLAY F 6
		Goto Missile
	Pain:
		PLAY G 4
		PLAY G 4 A_Pain
		Goto Spawn
	Death:
		PLAY H 0 A_PlayerSkinCheck("AltSkinDeath")
	Death1:
		PLAY H 10
		PLAY I 10 A_PlayerScream
		PLAY J 10 A_NoBlocking
		PLAY KLM 10
		PLAY N -1
		Stop
	XDeath:
		PLAY O 0 A_PlayerSkinCheck("AltSkinXDeath")
	XDeath1:
		PLAY O 5
		PLAY P 5 A_XScream
		PLAY Q 5 A_NoBlocking
		PLAY RSTUV 5
		PLAY W -1
		Stop
	AltSkinDeath:
		PLAY H 6
		PLAY I 6 A_PlayerScream
		PLAY JK 6
		PLAY L 6 A_NoBlocking
		PLAY MNO 6
		PLAY P -1
		Stop
	AltSkinXDeath:
		PLAY Q 5 A_PlayerScream
		PLAY R 0 A_NoBlocking
		PLAY R 5 A_SkullPop
		PLAY STUVWX 5
		PLAY Y -1
		Stop
	Pain.ChokeEM:
		PLAY G 1 A_GiveInventory("ChokeMe",1)
		TNT1 A 1 A_PlaySound("Hand/Choke")
		Goto See
	Pain.Silently:
		PLAY G 1 A_PlaySound("Null")
		Goto See
	Death.ChokeEM:
		PLAY H 1 A_TakeInventory("ChokeMe",1)
		PLAY H 1 A_GiveInventory("AmAlreadyChoked",1)
		PLAY H 1 A_SpawnItemEx("ChokingHand",10,50,30,0,0,0,0)
		Goto Death
	Death.Fire:
	Death.Burn:
	Death.Flare:
	Burn:
		PLAY A 0 A_Scream
		PLAY A 0 A_NoBlocking
		PLAY A 0 A_SpawnItem("BurnFormerHuman", 0, 7)
		TNT1 A -1
		Stop
	}
}

ACTOR ------CALEB-QUOTES---------- {}
ACTOR CultistSounder
{
	Height 1
	Radius 1
	+NOGRAVITY
	-TELESTOMP
	States
	{
	Spawn:
		TNT1 A 15
		TNT1 A 0 A_PlaysoundEx("CultistGotYou", 1)
		Stop
	}
}

ACTOR LaughDelay : PowerDamage // Given when Caleb does maniacal laugh for gibbing
{
	PowerUp.Duration -3
	DamageFactor "nothing", 1.0
}

ACTOR SpeechBase : CustomInventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 1
	+AUTOACTIVATE
	+ALWAYSPICKUP
}

ACTOR DoLaugh : SpeechBase
{
	States
	{
	Use:
	Pickup:
		TNT1 A 1 ACS_NamedExecuteAlways("CalebSpeech", 0, 0)
		Stop
	}
}

ACTOR CerberusDead : SpeechBase
{
	States
	{
	Use:
	Pickup:
		TNT1 A 1 ACS_NamedExecuteAlways("CalebSpeech", 0, 1)
		Stop
	}
}

ACTOR TchernobogDead : SpeechBase
{
	States
	{
	Use:
	Pickup:
		TNT1 A 1 ACS_NamedExecuteAlways("CalebSpeech", 0, 2)
		Stop
	}
}

ACTOR DoRat : SpeechBase
{
	States
	{
	Use:
	Pickup:
		TNT1 A 1 ACS_NamedExecuteAlways("CalebSpeech", 0, 3)
		Stop
	}
}

ACTOR DoTaunt : SpeechBase
{
	States
	{
	Use:
	Pickup:
		TNT1 A 0 ACS_NamedExecuteAlways("Jokes", 0, 0)
		Stop
	}
}

ACTOR DoJack : SpeechBase
{
	States
	{
	Use:
	Pickup:
		TNT1 A 1 ACS_NamedExecuteAlways("Jokes", 0, 1)
		Stop
	}
}

ACTOR ------WEREWOLF-TRANSFORM---------- {}
ACTOR StompingDamager
{
	Renderstyle None
	DamageType "Stomping"
	+BLOODLESSIMPACT
	+DONTSPLASH
	+NODAMAGETHRUST
	+NOTIMEFREEZE
	+PAINLESS
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(momz>0, 2)
		TNT1 A 0 A_Explode(random(1,-momz)+4,24,0)
		TNT1 A 0
		Stop
	}
}

ACTOR Werewolf : PlayerPawn
{
	Speed 2
	Health 666
	Radius 16
	Height 56
	Mass 300
	+NOSKIN
	-PICKUP
	PainChance 0
	Player.Face "WLF"
	Player.DisplayName "Werewolf"
	Player.CrouchSprite "XESC"
	Player.MorphWeapon "beasthands"
	Player.SoundClass "Werewolf"
	Player.JumpZ 16
	XScale 0.6
	YScale 0.52
	States
	{
	Spawn:
		BESW A -1
		Loop
	See:
		BESW ABCDF 4
		Loop
	Melee:
		BESC ABCDEFGHIJ 10
		Goto Spawn
	Pain:
		BESP A 4
		BESP A 4 A_Pain
		Goto Spawn
	Death:
		BESD A 10
		BESD B 10 A_PlayerScream
		BESD C 10 A_NoBlocking
		BESD DEF 10
		BESD F 1
		BESD FED 10
		BESD F -1
		Stop
	}
}