/*
 * Copyright (c) 2011-2021 Drugod, Ozymandias81
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

ACTOR ---------GARGOYLESOULS----------- {}
ACTOR GargoylSoul : BloomHybrid_Base 19005
{
	//$Title Gargoyle's Soul
	Health 100
	Radius 16
	Mass 50
	Damage 3
	PainChance 256
	//+DONTFALL
	+FLOAT
	+MISSILEMORE
	+NOGRAVITY
	+NOICEDEATH
	AttackSound "skull/Melee"
	PainSound "skull/pain"
	DeathSound "skull/death"
	ActiveSound "skull/active"
	RenderStyle SoulTrans
	Obituary "$OB_GSOUL"
	Species "Cacos"
	States
	{
	Spawn:
		GKUL AB 10 A_Look
		Loop
	See:
		GKUL AB 6 A_Chase
		Loop
	Missile:
		GKUL C 10 A_FaceTarget
		GKUL D 4 A_SkullAttack
		GKUL CD 4
		Goto Missile+2
	Pain:
		GKUL E 3
		GKUL E 3 A_Pain
		Goto See
	Death:
		GKUL F 6
		GKUL G 6 A_Scream
		GKUL H 6
		GKUL I 6 A_NoBlocking
		GKUL J 6
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_GiveToTarget("DoLaugh", 1)		
		GKUL K 6 A_SetFloorClip
		Stop
	Ice:
		GKUF R 0 A_SetMass(999999)
		GKUF R 5 A_FreezeDeath
		GKUF R 1 A_FreezeDeathChunks
		Wait
	}
}

ACTOR ----------ELEMENTAL_GARGOYLES---------- {}
ACTOR Elemental_Gargoyle : BloomHybrid_Base 19009
{
	//$Title GargoElemental
	Health 400
	Radius 31
	Mass 400
	PainChance 128
	+FLOAT
	+FLOATBOB
	+NOGRAVITY
	SeeSound "pain/sight"
	PainSound "pain/pain"
	DeathSound "pain/death"
	ActiveSound "pain/active"
	Species "Cacos"
	States
	{
	Spawn:
		PGIN A 10 A_Look
		Loop
	See:
		PGIN AABBCC 3 A_Chase
		Loop
	Missile:
		PGIN D 5 A_FaceTarget
		PGIN E 5 A_FaceTarget
		PGIN F 5 A_FaceTarget
		PGIN F 0 A_PainAttack("GargoylSoul")
		Goto See
	Pain:
		PGIN G 6
		PGIN G 6 A_Pain
		Goto See
	Death:
		PGIN H 8
		PGIN I 8 A_Scream
		PGIN JK 8
		PGIN L 8 A_PainDie("GargoylSoul")
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_GiveToTarget("DoLaugh", 1)		
		PGIN M 8 A_SetFloorClip
		Stop
	Raise:
		PGIN MLKJIH 8
		Goto See
	Ice:
		PGIF R 0 A_SetMass(999999)
		PGIF R 5 A_FreezeDeath
		PGIF R 1 A_FreezeDeathChunks
		Wait
	}
}