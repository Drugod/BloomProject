/*
 * Copyright (c) 2011-2021 Nash Muhandes, Ozymandias81
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

ACTOR NashGore_Blood : Blood replaces Blood
{
	States
	{
	Spawn:
		BLUD C 8
		BLUD B 8 A_SpawnItemEx("NashGore_FlyingBlood",0,0,8,random(-4,4),random(-4,4),random(2,5),0,143,SXF_ABSOLUTEVELOCITY|SXF_NOCHECKPOSITION)
		BLUD A 8
		Stop
	}
}

ACTOR NashGore_Bloodsplatter : NashGore_Blood replaces Bloodsplatter{}

ACTOR NashGore_BloodSpot
{
	Radius 12
	Height 2
	Mass 128
	+NOTELEPORT
	-NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(waterlevel > 0, "Underwater") //..so it will lag no more...
		TNT1 A 0 A_StartSound("bloodsplat")
		TNT1 A 0 A_Jump(64,4)
		TNT1 A 0 A_Jump(128,4)
		TNT1 A 0 A_Jump(192,4)
		TNT1 A 0 A_Jump(255,4)
		// make the blood spots stay on the ground. 1500 is the default duration.
		BSPT A 1500
		Stop
		BSPT B 1500
		Stop
		BSPT C 1500
		Stop
		BSPT D 1500
		Stop
	Underwater:	//..so it will lag no more...
		TNT1 A 0 A_StartSound("bloodsplat")
		TNT1 A 0 A_Jump(64,4)
		TNT1 A 0 A_Jump(128,4)
		TNT1 A 0 A_Jump(192,4)
		TNT1 A 0 A_Jump(255,4)
		// now the bloodspots will remain underwater only for a short time, then vanishes
		BSPT A 250
		Stop
		BSPT B 250
		Stop
		BSPT C 250
		Stop
		BSPT D 250
		Stop
	}
}

ACTOR NashGore_FlyingBlood
{
	Scale 0.75
	Health 1
	Radius 8
	Height 1
	Mass 1
	Gravity 0.75
	+CLIENTSIDEONLY
	+CORPSE
	+NOTELEPORT
	+NOBLOCKMAP
	States
	{
	Spawn:
		FBLD AABBCCDD 2 NODELAY A_SpawnItemEx("NashGore_FlyingBloodTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		Loop
	Crash:
		TNT1 A 0 A_StartSound("bloodsplat")
		TNT1 A 0 A_Jump(256,1,2,3,4)
		BSPT ABCD 0 A_Jump(256,"End")
		// make the blood spots stay on the ground. 1500 is the default duration.
		// Fadeout added by Zero Prophet for effect.
	End:
		BSPT "#" 1500
		BSPT "#" 1 A_FadeOut(0.02)
		Wait
	}
}

ACTOR NashGore_FlyingBloodTrail
{
	Scale 0.70
	Mass 1
	Gravity .125
	+NOBLOCKMAP
	+NOTELEPORT
	States
	{
	Spawn:
		BTRL ABCD 4
		Stop
	}
}