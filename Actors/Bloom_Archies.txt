/*
 * Copyright (c) 2011-2021 Drugod, Ozymandias81, IvanDobrovski/Cruduxy/Marinelol
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

ACTOR ----------ARCHZOMBIE---------- {}
ACTOR Gargovile : BloomHybrid_Base 19040
{
	//$Title Gargovile
	Health 700
	Radius 20
	Mass 500
	Speed 15
	GibHealth 30
	PainChance 10
	MaxTargetRange 896
	+DROPOFF
	+FLOORCLIP
	+NOTARGET
	+QUICKTORETALIATE
	SeeSound "vile/sight"
	PainSound "vile/pain"
	DeathSound "vile/death"
	ActiveSound "vile/active"
	MeleeSound "vile/Stop"
	Obituary "$OB_GAVILE"
	DropItem "FlameGas" 32
	Species "Archies"
	States
	{
	Spawn:
		ARCH A 10 A_Look
		Loop
	See:
		ARCH AABBCCDDEEFF 2 A_Chase
		TNT1 A 0 A_TakeInventory("CountZombie",9999) //let's reset the counter before any Summon - ozy81
		TNT1 A 0 A_CheckSight("Summon")
		ARCH AABBCCDDEEFF 2 A_Chase
		Loop
	Summon:
		ARCF J 1 ThrustThingZ(0,20,0,0)
		ARCF J 1 A_UnSetSolid
		TNT1 A 0 A_GiveInventory("CountZombie",1) //tell the engine that we are going to Summon - ozy81
		TNT1 A 0 A_JumpIfInventory("CountZombie",0,2) //no tokens? summon then, they will be added on any Summon - ozy81
		TNT1 A 0 A_ChangeFlag("COUNTKILL",0)
		ARCF J 1 A_SpawnItemEx("FlyingGargovile",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS)
		Stop
	Melee:
		ARCM A 0 A_Recoil(frandom(-2,0))
		ARCM A 0 A_PlaySound("GargoyleMelee")
		ARCM A 7 A_FaceTarget
		ARCM B 7 A_CustomMeleeAttack (frandom(3,6)*7,"imp/melee","imp/melee", "Stun",1)
		ARCM CBA 6
		Goto See
	Missile:
		TNT1 A 0 A_VileStart
		ARCH G 10 A_FaceTarget
		ARCH H 8 A_VileTarget
		ARCH IJKLMN 8 A_FaceTarget
		ARCH N 8 A_VileAttack
		ARCH N 20
		Goto See
	Pain:
		ARCH O 5
		ARCH O 5 A_Pain
		Goto See
	Death:
		ARCH P 7
		ARCH Q 7 A_Scream
		ARCH R 7 A_NoBlocking
		ARCH STUVW 7
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_GiveToTarget("DoLaugh", 1)
		ARCH X -1
		Stop
	Ice:
		ARCF R 0 A_SetMass(999999)
		ARCF R 5 A_FreezeDeath
		ARCF R 1 A_FreezeDeathChunks
		Wait
	}
}

ACTOR FlyingGargovile : BloomHybrid_Base 18322
{
	//$Title Flying Gargovile
	Health 700
	Radius 20
	Mass 500
	Speed 15
	GibHealth 30
	PainChance 255
	+FLOAT
	+FLOATBOB
	+NOGRAVITY
	PainSound "vile/pain"
	DeathSound "vile/death"
	ActiveSound "vile/active"
	MeleeSound "vile/Stop"
	Obituary "$OB_FLVILE"
	DropItem "FlameBigGas" 32
	DropItem "FlameThrower_" 8
	Species "Archies"
	States
	{
	Spawn:
		ARCF AABBCCAABBCC 2 A_Look
		Loop
	See:
		ARCF AABBCCAABBCC 2 A_Chase
		TNT1 A 0 A_TakeInventory("CountZombie",9999)
		TNT1 A 0 A_JumpIfHealthLower(500, "Summon")
		Goto See
	Summon:
		ARCF J 1 ThrustThingZ(0,20,0,0)
		ARCF J 1 A_Recoil(-20)
		ARCF J 1 A_UnSetSolid
		TNT1 A 0 A_GiveInventory("CountZombie",1)
		TNT1 A 0 A_JumpIfInventory("CountZombie",0,2)
		TNT1 A 0 A_ChangeFlag("COUNTKILL",0)
		ARCF J 1 A_SpawnItemEx("Gargovile",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS)
		Stop
	Melee:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_Recoil(frandom(-2,0))
		TNT1 A 0 A_PlaySound("GargoyleMelee")
		ARCF G 7 A_FaceTarget
		ARCF H 7 A_CustomMeleeAttack (frandom(3,6)*7,"imp/melee","imp/melee", "Stun",1)
		ARCF I 6
		Goto See
	Pain:
		ARCF J 5
		ARCF J 5 A_Pain
		Goto Summon
	Death:
		TNT1 A 0 A_ChangeFlag("FLOAT",0)
		TNT1 A 0 A_ChangeFlag("FLOATBOB",0)
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",0)
		ARCH P 7
		ARCH Q 7 A_Scream
		ARCH R 7 A_NoBlocking
		ARCH STUVW 7
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_GiveToTarget("DoLaugh", 1)
		ARCH X -1
		Stop
	Ice:
		ARCF R 0 A_SetMass(999999)
		ARCF R 5 A_FreezeDeath
		ARCF R 1 A_FreezeDeathChunks
		Wait
	}
}

ACTOR LivingGargovileStatue 19043
{
	//$Category BlooM Monsters
	//$Title Living Gargovile Statue
	//$Color 3
	Radius 10
	Height 35
	Mass 0x7ffffff
	Gravity 0.6
	+LOOKALLAROUND
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	-NOBLOOD
	-NOGRAVITY
	-SPAWNCEILING
	States
	{
	Spawn:
		ARCS A 320 A_SetTics (frandom((Health / 20)+950,2100))
		ARCS A 1 A_Look
		Loop
	See:
		ARCS A 1 A_JumpIfCloser(196,"Unmorph")
		TNT1 A 0 A_FaceTarget
		Loop
	Unmorph:
		ARCS BCD 5 A_FaceTarget
		TNT1 A 0 A_UnSetSolid
		ARCS E 1 A_SpawnItemEx("Gargovile",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS)
		Stop
	}
}

ACTOR ArchZombie : BloomHybrid_Base 19039
{
	//$Title ArchZombie
	Health 700
	Radius 20
	Mass 500
	Speed 15
	GibHealth 30
	PainChance 10
	MaxTargetRange 896
	+FLOORCLIP
	+NOTARGET
	+QUICKTORETALIATE
	SeeSound "vile/sight"
	PainSound "vile/pain"
	DeathSound "vile/death"
	ActiveSound "vile/active"
	MeleeSound "vile/Stop"
	Obituary "$OB_VILE"
	Species "Archies"
	States
	{
	Spawn:
		ARZM AB 10 A_Look
		Loop
	See:
		ARZM AABBCCDDEEFF 2 A_VileChase
		Loop
	Missile:
		TNT1 A 0 A_JumpIfInventory("CountZombie",10,2)
		TNT1 A 0 A_Jump(256,"Invocation")
		ARZM G 0 A_VileStart
		ARZM G 10 A_FaceTarget
		ARZM H 8 A_VileTarget
		ARZM IJKLMN 8 A_FaceTarget
		ARZM O 8 A_VileAttack
		ARZM P 20
		Goto See
	Invocation:
		TNT1 A 0 A_GiveInventory("CountZombie",1)
		TNT1 A 0 A_GiveInventory("AlreadyFlare",1)
		ARZM "[\]" 10 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("VileSpawner",frandom(32.0,64.0),frandom(32.0,64.0),0)
		TNT1 A 0 A_PlaySound("SummonZombies")
		TNT1 A 0 A_Jump(256,"Invocation1","Invocation2","Invocation3","Invocation4","Invocation5","Invocation6")
	Invocation1:
		TNT1 A 0 A_SpawnItemEx("FastBuriedGhoul1",frandom(32.0,64.0),frandom(32.0,64.0),0,0,0,0,0,SXF_SETMASTER)
		Goto See
	Invocation2:
		TNT1 A 0 A_SpawnItemEx("FastBuriedGhoul2",frandom(32.0,64.0),frandom(32.0,64.0),0,0,0,0,0,SXF_SETMASTER)
		Goto See
	Invocation3:
		TNT1 A 0 A_SpawnItemEx("FastBuriedGhoul3",frandom(32.0,64.0),frandom(32.0,64.0),0,0,0,0,0,SXF_SETMASTER)
		Goto See
	Invocation4:
		TNT1 A 0 A_SpawnItemEx("FastBuriedGhoul4",frandom(32.0,64.0),frandom(32.0,64.0),0,0,0,0,0,SXF_SETMASTER)
		Goto See
	Invocation5:
		TNT1 A 0 A_SpawnItemEx("FastBuriedGhoul5",frandom(32.0,64.0),frandom(32.0,64.0),0,0,0,0,0,SXF_SETMASTER)
		Goto See
	Invocation6:
		TNT1 A 0 A_SpawnItemEx("FastBuriedGhoul6",frandom(32.0,64.0),frandom(32.0,64.0),0,0,0,0,0,SXF_SETMASTER)
		Goto See
	Heal:
		ARZM "[\]" 10
		Goto See
	Pain:
		ARZM Q 5
		ARZM Q 5 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_Jump(128,"UltraDeath")
		ARZM Q 7
		ARZM R 7 A_Scream
		ARZM S 7 A_NoBlocking
		ARZM TUVWXY 7
		ARZM Z -1
		Stop
	XDeath:
		NOHX A 2
		NOHX B 2
		NOHX C 2 A_Quake(128,75,64,0)
		TNT1 A 0 A_SpawnItemEx("Blower",0,0,0)
		TNT1 AAA 0 A_SpawnDebris("OrganDebris")
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 A 0 A_SpawnDebris("OrganDebris2")
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 A 0 A_ThrowGrenade("KickableFlying",0,5,10,0)
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 A 0 A_SpawnDebris("Bloodliver")
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 A 0 A_SpawnDebris("Hand1")
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 A 0 A_SpawnItemEx("Intestines",frandom(-50,50),frandom(-50,50),0)
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 A 0 A_SpawnItemEx("Entrails",frandom(-50,50),frandom(-50,50),0)
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 AAAAAAA 0 A_SpawnDebris("FlyingBlood")
		TNT1 AAAAAAA 0 A_SpawnItemEx("Blood_",frandom(-20,20),frandom(-20,20),frandom(-20,20),frandom(0,15),frandom(0,15),frandom(0,15),frandom(0,360),0,120)
		NOHX DEFGH 4
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_GiveToTarget("DoLaugh", 1)
		NOHX I -1
		Stop
	UltraDeath:
		NOHA AAAAA 4 A_NoBlocking
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		NOHA BBBBB 4
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		NOHA C 45
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		NOHA DDDD 5
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		NOHA EEEE 5
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		NOHA F 45 A_ThrowGrenade("KickableFlying",5,0,0,0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_Fall
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_GiveToTarget("DoLaugh", 1)
		NOHE A 1 A_SpawnItemEx("ArchzombieHeadless",0,0,0)
		Stop
	Ice:
		ARZF Z 0 A_SetMass(999999)
		ARZF Z 5 A_FreezeDeath
		ARZF Z 1 A_FreezeDeathChunks
		Wait
	}
}

ACTOR ArchZombieHeadless
{
	Health 80
	Radius 20
	Height 56
	Mass 500
	Speed 15
	PainChance 10
	Monster
	+FRIENDLY
	+NOTARGET
	-COUNTKILL //just to force the no count - ozy81
	DeathSound "DSBDSPRY"
	Species "Archies"
	States
	{
	Spawn:
		NOHE A 10 A_Look
		TNT1 A 0 A_GiveInventory("CountToDeath",10)
		Loop
	See:
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		TNT1 A 0 A_ThrowGrenade("FlyingBlood",5,5+frandom(-3,3),10+frandom(-4,4),0)
		NOHE AABBCC 2 A_Chase
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_JumpIfInventory("CountToDeath",1,"See")
		Goto TriggerDeath
		TNT1 A 0 A_TakeInventory("CountToDeath",1)
		Loop
	Pain:
		NOHE D 4 A_Pain
		TNT1 A 0
		Goto See
	TriggerDeath:
		NOHE E 0 A_TakeInventory("CountToDeath",9999)
		TNT1 A 0 A_Die //make sure that thing gets killed
	Death:
		NOHE E 7
		NOHE F 7 A_Scream
		NOHE G 7 A_NoBlocking
		NOHE HIJKL 7
		NOHE M -1
		Stop
	XDeath:
		NOHX A 2 A_Die //make sure that thing gets killed
		NOHX B 2
		NOHX C 2 A_Quake(128,75,64,0)
		TNT1 A 0 A_SpawnItemEx("Blower",0,0,0)
		TNT1 AAA 0 A_SpawnDebris("OrganDebris")
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 A 0 A_SpawnDebris("OrganDebris2")
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 A 0 A_ThrowGrenade("KickableFlying",0,5,10,0)
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 A 0 A_SpawnDebris("Bloodliver")
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 A 0 A_SpawnDebris("Hand1")
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 A 0 A_SpawnItemEx("Intestines",frandom(-50,50),frandom(-50,50),0)
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 A 0 A_SpawnItemEx("Entrails",frandom(-50,50),frandom(-50,50),0)
		TNT1 A 0 A_SetAngle(frandom(0.0,360.0))
		TNT1 AAAAAAA 0 A_SpawnDebris("FlyingBlood")
		TNT1 AAAAAAA 0 A_SpawnItemEx("Blood_",frandom(-20,20),frandom(-20,20),frandom(-20,20),frandom(0,15),frandom(0,15),frandom(0,15),frandom(0,360),0,120)
		NOHX DEFGH 4
		NOHX I -1
		Stop
	Ice:
		ARZF R 0 A_SetMass(999999)
		ARZF R 5 A_FreezeDeath
		ARZF R 1 A_FreezeDeathChunks
		Wait
	}
}

ACTOR VileSpawner
{
	Radius 4
	Height 4
	Alpha 0.8
	RenderStyle Add
	+CANNOTPUSH
	+FLOORCLIP
	+RANDOMIZE
	-NOBLOCKMAP
	-NOGRAVITY
	States
	{
	Spawn:
		MUMM QRSTUVW 4
		Stop
	}
}