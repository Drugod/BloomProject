/*
 * Copyright (c) 2011-2021 Drugod, Ozymandias81, HorrorMovieGuy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

//3d bosses
Actor ChtonBloom 3405
{
	//$Category "Quake Stuff"
	//$Title Chton
	//$Color 13
	Height 128
	Radius 72
	Health 10000
	PainChance 2
	Speed 1
	Mass 0x7ffffff
	Monster
	+BOSSDEATH
	+CANTLEAVEFLOORPIC
	+MISSILEMORE
	+NOBLOODDECALS
	+NOBLOOD
	+NOICEDEATH
	SeeSound "Chton/Walk"
	ActiveSound "Chton/Rise"
	PainSound "Chton/Pain"
	DeathSound "Chton/Death"
	HitObituary "%o"
	Obituary "%o"
	States
	{
	Spawn:
		TNT1 A 0
		CHT1 A 10 A_Look
		Loop
	See: //rise frames should be used only 1 time here for proper effect
		TNT1 A 0 A_Playsound("Chton/Rise")
		CHT1 ABCDEFGHIJKLMNOPQ 6
		Goto Missile
		CHT2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 6 A_FaceTarget
		CHT3 ABCDE 6 A_FaceTarget
		Goto Missile
	Missile:
		CHT5 ABCDEFG 6 A_FaceTarget
		CHT5 H 0 A_PlaySound("Chton/Attack",4,frandom(1.0,2.0))
		CHT5 H 1 A_CustomMissile("Lavaball",84,72)
		CHT5 IJK 3 A_FaceTarget
		CHT5 LMNOPQR 6 A_FaceTarget
		CHT5 S 0 A_PlaySound("Chton/Attack",4,frandom(1.0,2.0))
		CHT5 S 1 A_CustomMissile("Lavaball",84,-72)
		CHT5 TUVW 3 A_FaceTarget
		Goto Missile
	Pain:
		TNT1 A 0 A_Jump(96,"Pain2","Pain3")
		CHT6 ABCDEFGHIJ 2 A_Pain
		TNT1 A 0 A_Jump(232,"Missile")
		Goto See+17
	Pain2:
		CHT7 ABCDEF 2 A_Pain
		TNT1 A 0 A_Jump(232,"Missile")
		Goto See+17
	Pain3:
		CHT8 ABCDEFGHIJ 2 A_Pain
		TNT1 A 0 A_Jump(232,"Missile")
		Goto See+17
	Death:
		CHT4 A 0 A_ScreamAndUnblock
		CHT4 ABCDEFGH 12
		CHT4 I 1 A_BossDeath
	Death.Fade:
		CHT4 I 1 A_Fadeout(0.1)
		Goto Death.Fade
	}
}

ACTOR Lavaball
{
	Decal Scorch
	Radius 11
	Height 8
	Speed 24
	Damage 10
	DamageType "Napalm"
	PROJECTILE
	+DONTSPLASH
	+EXTREMEDEATH
	+NOGRAVITY
	+RANDOMIZE
	DeathSound "LargeExplosions"
	ActiveSound "Old/DSNAPALM"
	States
	{
	Spawn:
		TNT1 A 0
		UNKN A 1
		TNT1 A 0 A_LoopActiveSound
	Fire:
		TNT1 A 0 A_JumpIf(waterlevel>0,"AltFire")
		UNKN AAAAAAAAAAAA 1 BRIGHT A_SpawnItem ("BSmokeTrail",0+frandom(-1,1),0+frandom(-1,1),0,0)
		Loop
	AltFire:
		UNKN AAAAAAAAAAAA 1 BRIGHT A_SpawnItemEx("SmokeTrailSmall")
		Goto Fire
	Death:
		UNKN A 1 BRIGHT A_Explode
		TNT1 A 0 A_SpawnItemEx("Kaboom",0,0,-15+frandom(-2,2),0,0,1,0,128)
		NAPX ABCDEFGHIJKLM 2 BRIGHT A_Quake(4,12,0,128)
		Stop
	}
}

ACTOR FlameQuake 3406
{
	//$Category "Quake Stuff"
	//$Title Flame Torch
	//$Color 13
	Radius 8
	Height 35
	+BRIGHT
	+NOGRAVITY
	Scale 1.5
	States
	{
	Spawn:
		TNT1 A 0
		FLQ1 ABCDEF 4
		Loop
	}
}

ACTOR QuakeColumns : SwitchingDecoration 3407
{
	//$Category "Quake Stuff"
	//$Title Columns Of Chton (Activateable)
	//$Color 13
	Radius 32
	Height 8
	+NOGRAVITY
	States
	{
	Spawn:
		UNKN A -1
		Stop
	Active:
		QCOL BCDEFGHIJKL 16 A_Quake(96,35*4,0,2048)
		Stop
	}
}

ACTOR RuneQuake : CustomInventory 3408
{
	//$Category "Quake Stuff"
	//$Title Rune
	//$Color 13
	Inventory.PickupMessage "$DIVESUIT"
	Inventory.PickupSound "misc/p_pkup"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	+COUNTITEM
	+DONTSPLASH
	+INVENTORY.FANCYPICKUPSOUND
	States
	{
	Spawn:
		TNT1 A 0
		UNKN A -1 BRIGHT
		Stop
	Use:
		UNKN A 1 BRIGHT
		Fail
	}
}