ACTOR -------BERSERK---------- {}
ACTOR Berserk_ : CustomInventory replaces Berserk //needed for distinct Caleb weapon - ozy81
{
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage "$GOTBERSERK"
	Inventory.PickupSound "misc/p_pkup"
	States
	{
	Spawn:
		PSTR A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIf(CheckClass("Caleb"), "Pickup2")
		TNT1 A 0 A_GiveInventory("PowerStrength")
		TNT1 A 0 HealThing(100, 0)
		TNT1 A 0 A_SelectWeapon("Fist_")
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("PowerStrength")
		TNT1 A 0 HealThing(100, 0)
		TNT1 A 0 A_SelectWeapon("Pitchfork")
		Stop
	}
}

ACTOR -------REFLECTIVESHOOTS---------- {}
ACTOR ReflectiveShots : CustomInventory 3164
{
	//$Category "BlooM Powerups"
	//$Title Reflective Shots
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupSound "PickMega"
	Inventory.PickupMessage "$REFLECT"
	States
	{
	Spawn:
		XEGA ABCDE 2 BRIGHT
		Loop
	Pickup:
		TNT1 A 0 A_GiveInventory ("SuperReflection")
		Stop
	}
}

ACTOR SuperReflection : PowerupGiver
{
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	PowerUp.Duration 1050
	PowerUp.Type "Reflection"
	Powerup.Colormap 0.5, 0.5, 0.6
}

ACTOR -------LIFESEED---------- {}
ACTOR LifeSeed : Health 3165
{
	//$Category "BlooM Powerups"
	//$Title Life seed
	+COUNTITEM
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	Inventory.Amount 100
	Inventory.MaxAmount 200
	Inventory.PickupMessage "$SEED"
	Inventory.PickupSound "items/heartpick"
	States
	{
	Spawn:
		XOUL ABCDEDCB 6 BRIGHT
		Loop
	}
}

ACTOR -------DEATHMASK---------- {}
ACTOR Deathmask : PowerupGiver 3151
{ 
	//$Category "BlooM Powerups"
	//$Title Death Mask
	Inventory.PickupMessage "$MASK"
	Inventory.MaxAmount 0
	+COUNTITEM
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	+FLOATBOB
	Scale 0.7
	Powerup.Color White 0.12
	PowerUp.Type "Invulnerable"  
	States
	{
	Spawn:
		XINV A -1 BRIGHT
		Stop
	}
}

ACTOR -------GUNSAKIMBO---------- {}
ACTOR GunsAkimbo : CustomInventory 3152
{ 
	//$Category "BlooM Powerups"
	//$Title Guns Akimbo
	Scale 0.6
	Inventory.PickupMessage "$AKIMBO"
	Inventory.PickupSound "misc/p_pkup"
	+COUNTITEM
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.FANCYPICKUPSOUND
	States
	{
	Spawn:
		XSTR A -1
		Stop
	Pickup:
		TNT1 A 0 A_GiveInventory ("PowerGunsAkimbo")
		Stop
	}
}

ACTOR PowerGunsAkimbo : PowerupGiver
{
	Scale 0.6 
	Inventory.MaxAmount 0
	PowerUp.Type "WeaponLevel2"
	PowerUp.Duration 1250
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Spawn:
		XSTR A -1
		Stop
	}
}

ACTOR ----------RESTRICTED---------- {}
ACTOR UnholyBible_ : PowerupGiver 18610 //DON'T USE THIS IN COOP - Ozy81
{
	//$Category "BlooM Powerups"
	//$Title Unholy Bible (Doomguy Only)
	Powerup.Color Navy 0.25
	PowerUp.Type UnholyBible_
	PowerUp.Duration 1250
	+COUNTITEM
	+FLOATBOB
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.FANCYPICKUPSOUND
	+INVENTORY.RESTRICTABSOLUTELY
	+NOTDMATCH
	Inventory.PickupMessage "$BIBLE"
	Inventory.PickupSound "PICKQUAD"
	Inventory.RestrictedTo "DoomDude"
	VisibleToPlayerClass "DoomDude"
	States
	{
	Spawn:
		UNHB A -1
		Stop
	}
}

ACTOR PowerUnholyBible_ : PowerDamage { DamageFactor "normal", 4 }

ACTOR BeastForm : CustomInventory 18611 //DON'T USE THIS IN COOP - Ozy81
{
	//$Category "BlooM Powerups"
	//$Title Beast Form (Caleb Only)
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.RESTRICTABSOLUTELY
	+NOTDMATCH
	Inventory.PickupMessage "$BEASTFORM"
	Inventory.PickupSound "misc/p_pkup"
	Inventory.RestrictedTo "Caleb"
	VisibleToPlayerClass "Caleb"
	States
	{
	Spawn:
		BEST A -1
		Stop
	Pickup:
		TNT1 A 1 A_Playsound("DSCULT2")
		TNT1 A 1 A_GiveInventory("WolfMe")
		Stop
	}
}

ACTOR WolfMe : PowerupGiver
{ 
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	PowerUp.Duration 1050
	PowerUp.Type "Wolf"
	Powerup.Color RedMap
}

ACTOR PowerWolf : PowerMorph
{
	PowerMorph.PlayerClass "werewolf"
	PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_ADDSTAMINA)
	PowerMorph.MorphFlash "WolfMorph"
	PowerMorph.Unmorphflash "WolfMorph"
}

ACTOR WolfMorph
{
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	}
} 