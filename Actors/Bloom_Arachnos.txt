ACTOR ----------ARACHNOIDS---------- {}
ACTOR Arachnoid : BloomHybrid_Base 19011
{
	//$Title Arachnoid
	Health 150
	PainChance 180
	FastSpeed 12
	Radius 30
	Mass 80
	+BOSSDEATH
	+DROPOFF
	Obituary "$OB_ACHNOID"
	Attacksound "dsdolstb"
	PainSound "SpiderPain"
	DeathSound "baby/death"//DeathSound "SpiderDie"
	ActiveSound "SpiderSee"
	States
	{
	Spawn:
		ARND A 5 A_Look
		Loop
	See:
		ARND AAAAABBBBB 1 A_Chase
		ARND CCCCCDDDDD 1 A_Chase
		ARND A 0 A_JumpIfCloser(random(200,400),"Bounce")
		ARND A 0 A_CheckFloor("Jump")
		Loop
	Bounce:
		ARND A 0 A_FaceTarget
		ARND A 0 A_Recoil(-4)
		ARND AAA 1 ThrustThingZ(0,frandom(11,20),0,0)
		ARND BBB 1 ThrustThing(angle*256/360,random(5,8),0,0)
		Goto See
	Jump:
		ARND A 0 A_Recoil(-4)
		ARND A 0 ThrustThingZ(0,frandom(11,20),0,0)
		ARND A 0 ThrustThing(angle*256/360,random(5,8),0,0)
		Goto See
	Melee:
		ARND F 3
		ARND G 6 A_CustomMeleeAttack(frandom(1,2),"dsdolstb","none","Turner",true)
		Goto See
	Pain:
		ARND I 3
		ARND I 3 A_Pain
		Goto See
	XDeath:
	Death:
		ARND J 0 A_Scream
		ARND JKLMNOP 3 A_SpawnDebris("FlyingBlood")
		TNT1 A 0 A_NoBlocking
		ARND P 1 A_SpawnDebris("FlyingBlood")
		ARND P -1 A_BossDeath
		Stop
	}
}

ACTOR Green_Arachnopod : BloomHybrid_Base 19070
{
	//$Title Green Arachnopod
    Health 500
    Radius 64
    Height 64
    Mass 600
    Speed 12
    PainChance 128
    Monster
    +FLOORCLIP
    +BOSSDEATH
	+LOOKALLAROUND
	+JUMPDOWN
    SeeSound "baby/sight"
    PainSound "baby/pain"
    DeathSound "baby/death"
    ActiveSound "baby/active"
	Obituary "$OB_ARAPOD"
	States
	{
	Spawn:
		APOD A 5 A_Look
		Loop
	See:
		TNT1 A 0 	
		APOD A 20 A_Jump(230,3)
		TNT1 A 0 A_JumpIfCloser(400.0,"Bounce")
		TNT1 A 0 A_CheckFloor("Jump")				
		APOD A 3 A_BabyMetal
		APOD ABBCC 3 A_Chase
		APOD D 3 A_BabyMetal
		APOD DEEFF 3 A_Chase
	Goto See+2
	Bounce:
		APOD J 5 A_FaceTarget
		APOD K 5
		APOD KKK 1 ThrustThingZ(0,frandom(75,50),0,0)
		APOD LLL 1 ThrustThing(angle*256/360,random(5,8),0,0)
		Goto See
	Jump:
		APOD J 5
		APOD K 5 ThrustThingZ(0,frandom(50,75),0,0)
		APOD L 5 ThrustThing(angle*256/360,random(5,8),0,0)
		Goto See
	Missile:
		APOD A 20 A_FaceTarget
		APOD G 4
		APOD H 4 A_CustomMissile("ButcherVomit", 58)
		APOD I 1 
    Goto See
	Pain:
		APOD J 3
		APOD J 3 A_Pain
		Goto See
	XDeath:
	Death:
		APOD J 20 A_Scream
		APOD N 7 A_NoBlocking
		APOD OPQRS 7
		APOD T -1 A_BossDeath
    Stop
	}
}