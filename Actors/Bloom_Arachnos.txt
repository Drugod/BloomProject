/*
 * Copyright (c) 2011-2021 Drugod, Ozymandias81, IvanDobrovski/Cruduxy/Marinelol
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

ACTOR ----------ARACHNOIDS---------- {}
ACTOR Arachnoid : BloomHybrid_Base 19011
{
	//$Title Arachnoid
	Health 150
	PainChance 180
	FastSpeed 12
	Radius 30
	Mass 80
	+BOSSDEATH
	+DROPOFF
	+THRUSPECIES //so they don't pile when they jump over  - ozy81
	Obituary "$OB_ACHNOID"
	Attacksound "dsdolstb"
	PainSound "SpiderPain"
	DeathSound "baby/death"//DeathSound "SpiderDie"
	ActiveSound "SpiderSee"
	Species "Spiders"
	States
	{
	Spawn:
		ARND A 5 A_Look
		Loop
	See:
		ARND AAAAABBBBB 1 A_Chase
		ARND CCCCCDDDDD 1 A_Chase
		ARND A 0 A_JumpIfCloser(random(200,400),"Bounce")
		ARND A 0 A_CheckFloor("Jump")
		Loop
	Bounce:
		ARND A 0 A_FaceTarget
		ARND A 0 A_Recoil(-4)
		ARND AAA 1 ThrustThingZ(0,frandom(11,20),0,0)
		ARND BBB 1 ThrustThing(angle*256/360,random(5,8),0,0)
		Goto See
	Jump:
		ARND A 0 A_Recoil(-4)
		ARND A 0 ThrustThingZ(0,frandom(11,20),0,0)
		ARND A 0 ThrustThing(angle*256/360,random(5,8),0,0)
		Goto See
	Melee:
		ARND F 3
		ARND G 6 A_CustomMeleeAttack(frandom(1,2),"dsdolstb","none","Turner",true)
		Goto See
	Pain:
		ARND I 3
		ARND I 3 A_Pain
		Goto See
	XDeath:
	Death:
		ARND J 2 A_Scream
		ARND JKLMNOP 3 A_SpawnDebris("FlyingBlood")
		TNT1 A 0 A_NoBlocking
		ARND P 1 A_SpawnDebris("FlyingBlood")
		ARND P -1 A_BossDeath
		Stop
	Ice:
		ARND R 0 A_SetMass(999999)
		ARND R 5 A_FreezeDeath
		ARND R 1 A_FreezeDeathChunks
		Wait
	}
}

ACTOR Green_Arachnopod : BloomHybrid_Base 19070
{
	//$Title Green Arachnopod
    Health 500
    Radius 64
    Height 64
    Mass 600
    Speed 12
    PainChance 128
    Monster
    +FLOORCLIP
    +BOSSDEATH
	+LOOKALLAROUND
	+JUMPDOWN
	+THRUSPECIES //so they don't pile when they jump over  - ozy81
	DamageFactor "Ice", 1.2
	DamageFactor "Fire", 1.2
	DamageFactor "Flare", 1.2
	DamageFactor "SpecialFire", 1.2
    SeeSound "baby/sight"
    PainSound "baby/pain"
    DeathSound "baby/death"
    ActiveSound "baby/active"
	Obituary "$OB_ARAPOD"
	Species "Spiders"
	States
	{
	Spawn:
		APOD A 5 A_Look
		Loop
	See:
		TNT1 A 0
		APOD A 20 A_Jump(230,3)
		TNT1 A 0 A_JumpIfCloser(400.0,"Bounce")
		TNT1 A 0 A_CheckFloor("Jump")
		TNT1 A 0 A_Jumpif(waterlevel> 0, "JUMP")
		APOD A 3 A_BabyMetal
		APOD ABBCC 3 A_Chase
		APOD D 3 A_BabyMetal
		APOD DEEFF 3 A_Chase
		Goto See+2
	Bounce:
		APOD J 5 A_FaceTarget
		APOD K 5 A_FaceTarget
		APOD KKK 1 ThrustThingZ(0,frandom(50,75),0,0)
		APOD LLL 1 ThrustThing(angle*256/360,random(5,8),0,0)
		Goto See
	Jump:
		APOD J 5 A_FaceTarget
		APOD K 5 ThrustThingZ(0,frandom(50,75),0,0)
		APOD L 5 ThrustThing(angle*256/360,random(5,8),0,0)
		Goto See
	Missile:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("CountBramble",10,2)
		APOD A 5 A_Jump(128,"DoBramble")
		APOD G 4 A_FaceTarget
		APOD H 4 A_CustomMissile("ButcherVomit", 58)
		APOD I 1
		Goto See
	DoBramble:
		TNT1 A 0 A_GiveInventory("CountBramble",1)
		TNT1 A 0 A_PlaySound("TentacleUp")
		APOX A 3 A_SpawnItemEx("GreenArachnoBramble",frandom(32.0,64.0), frandom(32.0,64.0), 0.0, 0.0, 0.0, 0.0, 0.0, SXF_SETMASTER,0,tid)
		APOX ABBCC 3 A_Chase
		APOX D 3 A_BabyMetal
		APOX DBBCC 3 A_Chase
		Goto See
	Pain:
		APOD J 3
		APOD J 3 A_Pain
		Goto See
	XDeath:
	Death:
		APOD J 2 A_Scream
		APOD N 7 A_NoBlocking
		APOD OPQRS 7 A_KillChildren
		APOD T -1 A_BossDeath
		Stop
	Ice:
		FPOD A 0 A_SetMass(999999)
		FPOD A 5 A_FreezeDeath
		FPOD A 1 A_FreezeDeathChunks
		Wait
	}
}

ACTOR GreenArachnoBramble : HellroseBramble
{
	Height 64
	Radius 8
	Health 10
	Scale 0.7
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteAlways("FloorTextureCheck", 0,tid)
	Deciding:
		TNT1 A 1
		Loop
	NoGroundSpawner:
		TNT1 A 0
		Stop
	GroundSpawner:
		TNT1 A 0
		TNT1 A 0 ACS_NamedSuspend("FloorTextureCheck", 0)
		TNT1 A 1 A_Look
		Loop
	See:
		TNT1 A 1 A_Warp(AAPTR_TARGET,frandom(40.0,60.0),frandom(40.0,60.0),0.0,0.0,0,"Raising")
		Loop
	Raising:
		TNT1 AAAA 8 A_SpawnItemEx("Drt", 0, 0, 0, frandom(-3.0,3.0), frandom(-3.0,3.0), 3.0, 0.0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		GVIN RQPONMLK 4 A_SpawnItemEx("Drt", 0.0, 0.0, 0.0, frandom(-3.0,3.0), frandom(-3.0,3.0), 3.0, 0.0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 0 A_SetShootable
		TNT1 A 0 A_SetSolid
		Goto See2
	See2:
		TNT1 A 0 A_FaceTarget
		GVIN ABCDEFGHIJKL 8 A_Chase
		Goto See2
	Melee:
		GVIN AB 4 A_FaceTarget
		GVIN C 4 A_MeleeAttack
		GVIN DEF 4
		Goto See2
	Pain:
		GVIN LMNOPQR 3
		Goto See2
	Death:
		TNT1 A 0 A_TakeInventory("CountBramble",1,0,AAPTR_MASTER)
		GVIN S 5
		GVIN T 5 A_Scream
		GVIN U 5 A_Fall
		GVIN V 5
		Stop
	}
}

ACTOR Magma_Arachnopod : Green_Arachnopod 19071
{
	//$Title Magma Arachnopod
	DamageFactor "Ice", 2.0
	DamageFactor "Fire", 0.0
	DamageFactor "Flare", 0.0
	DamageFactor "SpecialFire", 0.0
	DropItem "FlameGas"
	DropItem "FlameThrower_" 100
	Species "Spiders"
	States
	{
	Spawn:
		MPOD A 5 A_Look
		Loop
	See:
		TNT1 A 0
		MPOD A 20 A_Jump(230,3)
		TNT1 A 0 A_JumpIfCloser(400.0,"Bounce")
		TNT1 A 0 A_CheckFloor("Jump")
		TNT1 A 0 A_Jumpif(waterlevel> 0, "JUMP")
		MPOD A 3 A_BabyMetal
		TNT1 AAAAA 0 A_SpawnItemEx("BFireTrail", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		MPOD ABBCC 3 A_Chase
		TNT1 AAA   0 A_SpawnItemEx("BFireTrail2", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		MPOD D 3 A_BabyMetal
		TNT1 AAAA  0 A_SpawnItemEx("BFireTrail", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		MPOD DEEFF 3 A_Chase
		TNT1 AAAAA 0 A_SpawnItemEx("BFireTrail2", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Goto See+2
	Bounce:
		MPOD J 5 A_FaceTarget
		MPOD K 5 A_FaceTarget
		MPOD KKK 1 ThrustThingZ(0,frandom(50,75),0,0)
		MPOD LLL 1 ThrustThing(angle*256/360,random(5,8),0,0)
		Goto See
	Jump:
		MPOJ A 5 A_FaceTarget
		MPOJ B 5 ThrustThingZ(0,frandom(50,75),0,0)
		MPOJ C 5 ThrustThing(angle*256/360,random(5,8),0,0)
		Goto See
	Missile:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("CountBramble",10,2)
		MPOD A 5 A_Jump(128,"DoBramble")
		MPOD G 4 A_FaceTarget
		TNT1 AAA 0 A_SpawnItemEx("BFireTrail", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 AAAA 0 A_SpawnItemEx("BFireTrail2", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 AAAAA 0 A_SpawnItemEx("BFireTrail", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		MPOD HI 4 A_FaceTarget
		TNT1 A 0 A_Playsound ("PodSpit")
		TNT1 AAA 0 A_SpawnItemEx("BFireTrail", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 AAAA 0 A_SpawnItemEx("BFireTrail2", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 AAAAA 0 A_SpawnItemEx("BFireTrail", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		MPOD JK 4 A_CustomMissile("LavaBarf",40,0,frandom(-10,10),CMF_AIMDIRECTION,(ACS_NamedExecuteWithResult("SetActivatorTarget", 0, tidtohate)*10)/frandom(20,40))
		TNT1 AAAAA 0 A_SpawnItemEx("BFireTrail2", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 AAAA 0 A_SpawnItemEx("BFireTrail", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 AAA 0 A_SpawnItemEx("BFireTrail2", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		MPOD H 1
		Goto See
	DoBramble:
		TNT1 A 0 A_GiveInventory("CountBramble",1)
		TNT1 A 0 A_PlaySound("TentacleUp")
		TNT1 AAAAA 0 A_SpawnItemEx("BFireTrail", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		MPOX A 3 A_SpawnItemEx("MagmaBramble",frandom(32.0,64.0), frandom(32.0,64.0), 0.0, 0.0, 0.0, 0.0, 0.0, SXF_SETMASTER,0,tid)
		TNT1 AAA 0 A_SpawnItemEx("BFireTrail2", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		MPOX ABBCC 3 A_Chase
		TNT1 AAAA 0 A_SpawnItemEx("BFireTrail", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		MPOX D 3 A_BabyMetal
		TNT1 AAAAA 0 A_SpawnItemEx("BFireTrail2", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		MPOX DBBCC 3 A_Chase
		TNT1 AAA 0 A_SpawnItemEx("BFireTrail", frandom(-4,4), frandom(-4,4), frandom(32,40), frandom[HellRose](-3,3), frandom[HellRose](-3,3), 3, 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Goto See
	Pain:
		MPOD J 3
		MPOD J 3 A_Pain
		Goto See
	XDeath:
	Death:
		MPOD J 2 A_Scream
		MPOD N 7 A_NoBlocking
		MPOD OPQRS 7 A_KillChildren
		MPOD T -1 A_BossDeath
		Stop
	}
}

ACTOR MagmaBramble : HellroseBramble
{
	Height 64
	Radius 8
	Health 10
	Scale 0.7
	DamageFactor "Ice", 2.0
	DamageFactor "Fire", 0.0
	DamageFactor "Flare", 0.0
	DamageFactor "SpecialFire", 0.0
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteAlways("LavaTextureCheck", 0,tid)
	Deciding:
		TNT1 A 1
		Loop
	NoGroundSpawner:
		TNT1 A 0
		Stop
	GroundSpawner:
		TNT1 A 0
		TNT1 A 0 ACS_NamedSuspend("LavaTextureCheck", 0)
		TNT1 A 1 A_Look
		Loop
	See:
		TNT1 A 1 A_Warp(AAPTR_TARGET,frandom(40.0,60.0),frandom(40.0,60.0),0.0,0.0,0,"Raising")
		Loop
	Raising:
		TNT1 AAAA 8 A_SpawnItemEx("Drt", 0, 0, 0, frandom(-3.0,3.0), frandom(-3.0,3.0), 3.0, 0.0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		LVIN RQPONMLK 4 A_SpawnItemEx("Drt", 0.0, 0.0, 0.0, frandom(-3.0,3.0), frandom(-3.0,3.0), 3.0, 0.0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 0 A_SetShootable
		TNT1 A 0 A_SetSolid
		Goto See2
	See2:
		TNT1 A 0 A_FaceTarget
		LVIN ABCDEFGHIJKL 8 A_Chase
		Goto See2
	Melee:
		LVIN AB 4 A_FaceTarget
		LVIN C 4 A_MeleeAttack
		LVIN DEF 4
		Goto See2
	Missile:
		LVIN GF 4 A_FaceTarget
		LVIN E 4 A_CustomMissile("HellHoundFire",20,0,frandom(-20,20),0)
		LVIN FG 4
		Goto See2
	Pain:
		LVIN LMNOPQR 3
		Goto See2
	Death:
		TNT1 A 0 A_TakeInventory("CountBramble",1,0,AAPTR_MASTER)
		LVIN S 5
		LVIN T 5 A_Scream
		LVIN U 5 A_Fall
		LVIN V 5
		Stop
	}
}

ACTOR Ice_Arachnopod : Green_Arachnopod 19072
{
	//$Title Ice Arachnopod
	DamageFactor "Ice", 0.0
	DamageFactor "Fire", 2.0
	DamageFactor "Flare", 1.5
	DamageFactor "SpecialFire", 1.5
	DropItem "Frostgun_" 100
	DropItem "FrostAmmo"
	Species "Spiders"
	States
	{
	Spawn:
		IPOD A 5 A_Look
		Loop
	See:
		TNT1 A 0
		IPOD A 20 A_Jump(230,3)
		TNT1 A 0 A_JumpIfCloser(400.0,"Bounce")
		TNT1 A 0 A_CheckFloor("Jump")
		TNT1 A 0 A_Jumpif(waterlevel> 0, "JUMP")
		IPOD A 3 A_BabyMetal
		IPOD ABBCC 3 A_Chase
		IPOD D 3 A_BabyMetal
		IPOD DEEFF 3 A_Chase
		Goto See+2
	Bounce:
		IPOD J 5 A_FaceTarget
		IPOD K 5 A_FaceTarget
		IPOD KKK 1 ThrustThingZ(0,frandom(50,75),0,0)
		IPOD LLL 1 ThrustThing(angle*256/360,random(5,8),0,0)
		Goto See
	Jump:
		IPOJ A 5 A_FaceTarget
		IPOJ B 5 ThrustThingZ(0,frandom(50,75),0,0)
		IPOJ C 5 ThrustThing(angle*256/360,random(5,8),0,0)
		Goto See
	Missile:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("CountBramble",10,2)
		IPOD A 5 A_Jump(128,"DoBramble")
		IPOD G 4 A_FaceTarget
		IPOD HIJKJI 4 A_CustomMissile("IcePodFX", 58)
		IPOD I 1
		Goto See
	DoBramble:
		TNT1 A 0 A_GiveInventory("CountBramble",1)
		TNT1 A 0 A_PlaySound("TentacleUp")
		IPOX A 3 A_SpawnItemEx("IceBramble",frandom(32.0,64.0), frandom(32.0,64.0), 0.0, 0.0, 0.0, 0.0, 0.0, SXF_SETMASTER,0,tid)
		IPOX ABBCC 3 A_Chase
		IPOX D 3 A_BabyMetal
		IPOX DBBCC 3 A_Chase
		Goto See
	Pain:
		IPOD J 3
		IPOD J 3 A_Pain
		Goto See
	XDeath:
	Death:
		IPOD J 2 A_Scream
		IPOD N 7 A_NoBlocking
		IPOD OPQRS 7 A_KillChildren
		IPOD T -1 A_BossDeath
		Stop
	}
}

ACTOR IceBramble : HellroseBramble
{
	Height 64
	Radius 8
	Health 10
	Scale 0.7
	DamageFactor "Ice", 0.0
	Translation "Ice"
	Species "Bramble"
	+THRUSPECIES
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteAlways("IceTextureCheck", 0,tid)
	Deciding:
		TNT1 A 1
		Loop
	NoGroundSpawner:
		TNT1 A 0
		Stop
	GroundSpawner:
		TNT1 A 0
		TNT1 A 0 ACS_NamedSuspend("IceTextureCheck", 0)
		TNT1 A 1 A_Look
		Loop
	See:
		TNT1 A 1 A_Warp(AAPTR_TARGET,frandom(40.0,60.0),frandom(40.0,60.0),0.0,0.0,0,"Raising")
		Loop
	Raising:
		TNT1 AAAA 8 A_SpawnItemEx("Drt", 0, 0, 0, frandom(-3.0,3.0), frandom(-3.0,3.0), 3.0, 0.0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION)
		GVIN RQPONMLK 4 A_SpawnItemEx("Drt", 0.0, 0.0, 0.0, frandom(-3.0,3.0), frandom(-3.0,3.0), 3.0, 0.0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_SetShootable
		TNT1 A 0 A_SetSolid
		Goto See2
	See2:
		TNT1 A 0 A_FaceTarget
		GVIN ABCDEFGHIJKL 8 A_Chase
		Goto See2
	Melee:
		GVIN AB 4 A_FaceTarget
		GVIN C 4 A_MeleeAttack
		GVIN DEF 4
		Goto See2
	Missile:
		GVIN GF 4 A_FaceTarget
		GVIN E 4 A_CustomMissile("BrambleFX", 38)
		GVIN FG 4
		GVIN GF 4 A_FaceTarget
		GVIN E 4 A_CustomMissile("BrambleFX", 38)
		GVIN FG 4
		Goto See2
	Pain:
		GVIN LMNOPQR 3
		Goto See2
	Death:
		TNT1 A 0 A_TakeInventory("CountBramble",1,0,AAPTR_MASTER)
		GVIN S 5
		GVIN T 5 A_Scream
		GVIN U 5 A_Fall
		GVIN V 5
		Stop
	}
}

ACTOR RomeroTron : BloomHybrid_Base 19137
{
	//$Title RomeroTron
    Health 1500 //was 500, added more health... maybe Boss? --ozy81
    Radius 64
    Height 88
    Mass 600
    Speed 36
    PainChance 128
    Monster
    +FLOORCLIP
    +BOSSDEATH
	+JUMPDOWN
	+NOTARGET
	+THRUSPECIES //so they don't pile when they jump over  - ozy81
    SeeSound "trn/active"
    PainSound "baby/pain"
    DeathSound "baby/death"
    ActiveSound "trn/active"
	AttackSound "chainguy/attack"
	Obituary "$OB_ROMTRON"
	Species "Spiders"
	States
	{
	Spawn:
		RSPI A 5 A_Look
		Loop
	See:
		RSPI A 20
		RSPI A 3 A_BabyMetal
		RSPI ABBCC 3 A_Chase
		RSPI D 3 A_BabyMetal
		RSPI DEE 3 A_Chase
		Goto See+1
	Missile: //Use LifeLeech attack
		TNT1 A 0 //Needed to not avoid the jump frame
		RSPI G 0 A_JumpIfHealthLower(900, "Missile4")
		RSPI G 0 A_Jump(32,"Missile4")
		RSPI G 0 A_Jump(128,"Missile2","Missile3")
		RSPI G 10 A_FaceTarget
		RSPI GH 4 A_CustomMissile("LifeLeechShot",20,0,frandom(-5,5),0)
		RSPI H 1 A_MonsterRefire(128,"see")
		Goto Missile+1
	Missile2: //Use TeslaCannon attack
		RSPI Q 10 A_FaceTarget
		RSPI QR 4 A_CustomMissile("TeslaProjectile",20,0,frandom(-20,20),0)
		RSPI R 4 A_CustomMissile("TeslaProjectile",20,0,frandom(-20,20),0)
		RSPI R 1 A_MonsterRefire(128,"see")
		Goto Missile2+1
	Missile3: //Use TeslaCannon alt attack
		RSPI Q 20 A_FaceTarget
		RSPI Q 0 A_CustomMissile("TeslaBallBig",20,0,frandom(-2,2),0)
		RSPI QR 8 A_FaceTarget
		RSPI R 1 A_MonsterRefire(64,"see")
		Goto Missile3+1
	Missile4: //Use Spawn a icon of sin cube
		RSPI I 18 A_BrainAwake		
	    RSPI I 1 A_CustomMissile("RomeroSpawnShot")		
		Goto See
	Pain:
		RSPI I 3
		RSPI I 3 A_Pain
		Goto See
	XDeath:
	Death:
		RSPI J 2 A_Scream
		RSPI K 7 A_NoBlocking
		RSPI LMNOP 7 A_KillChildren //why KillChildren? --ozy81
		RSPI P -1 A_BossDeath
		Stop
	Ice:
		FSPI A 0 A_SetMass(999999)
		FSPI A 5 A_FreezeDeath
		FSPI A 1 A_FreezeDeathChunks
		Wait
	}
}

ACTOR RomeroSpawnShot
{
	Radius 6
	Height 32
	Speed 10
	Damage 3
	Projectile
	+NOCLIP
	-ACTIVATEPCROSS
	+RANDOMIZE
	SeeSound "brain/spit"
	DeathSound "brain/cubeboom"
	Dropitem "RomeroEye"
	States
	{
	Spawn:
		BOSF A 3 Bright A_SpawnSound
		BOSF BCD 3 Bright A_SpawnFly
		Loop
	}
}

ACTOR RomeroEye : RandomSpawner //potential mapbreaker! - ozy81
{
	DropItem "ShotgunMonsters"
	DropItem "ChaingunMonsters"
	DropItem "Eelmonsters"
	DropItem "ElementalGarmonsters"
	DropItem "HoundSpawner"
	DropItem "SpectreSpawner"
	DropItem "Garmonsters"
	DropItem "GhostandRevenants"
	DropItem "TheSouls"
	DropItem "TheArachnoids"
	DropItem "TheCvltimpZombImps"
	DropItem "TheWolfKnights"
	DropItem "TheGhoulsZombies"
	DropItem "TheArchZombies"
	DropItem "TheHellButchers"
}